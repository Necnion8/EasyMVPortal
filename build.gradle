import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.0"
}

group = 'com.gmail.necnionch.myplugin.easymvportal'
version = '1.1.2+1.21.4'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        url = "https://jitpack.io"
    }

    maven {
        name = "codemc-snapshots"
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'

    compileOnly files("lib/Multiverse-Core-4.2.2.jar")
    compileOnly files("lib/Multiverse-Portals-4.2.0.jar")
    compileOnly files("lib/worldedit-bukkit-7.1.0.jar")

    implementation 'net.wesjd:anvilgui:1.10.4-SNAPSHOT'
}

tasks.named("shadowJar", ShadowJar) {
    archiveClassifier.set("")
    dependencies {
        exclude "/META-INF/**"
    }
    relocate "net.wesjd.anvilgui", "${project.group}.libs.anvilgui"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}